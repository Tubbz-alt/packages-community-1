# Maintainer: Stefano Capitani <stefano@manjaro.org>

_pkgbase=papirus-icon-theme
pkgname=papirus-maia-icon-theme
pkgver=20180401
pkgrel=1
pkgdesc="Manjaro variation of Papirus icon theme"
arch=('any')
url="https://github.com/PapirusDevelopmentTeam/papirus-icon-theme"
license=("LGPL3")
depends=('gtk-update-icon-cache')
conflicts=("$_pkgbase"
	"$_pkgbase-git"
	"papirus-gtk-icon-theme"
    "papirus-gtk-icon-theme-git"
    "papirus-icon-theme-gtk"
    "papirus-icon-theme-gtk-git"
    "papirus-icon-theme-kde"
    "papirus-icon-theme-kde-git")
options=('!strip')
source=("$_pkgbase-$pkgver.tar.gz::https://github.com/PapirusDevelopmentTeam/$_pkgbase/archive/$pkgver.tar.gz")
sha512sums=('96b2ffecf55b36be108b68157bec60071112d49e4c03a8dd54e3e271301ad2dc6d672bb493594bd88f8903f8cf044995d075fdafe62c38c0cfcc10cfa55734a7')

prepare() {
	cd $_pkgbase-$pkgver

#Change color for Manjaro

msg "Create Papirus-Maia: this next bit might take a little while..."

	find . -type f -name '*.*' -exec sed -i "s|#4877b1|#3c8d7d|Ig;\
											s|#5294e2|#16a085|Ig;\
											s|#1d344f|#266357|Ig;\
											s|#0096aa|#188694|Ig;\
											s|#00bcd4|#16a085|Ig" {} \;
msg "Done Manjarifications"

#Change Icons folders name
	sed -i -e 's/ePapirus Papirus Papirus-Adapta Papirus-Adapta-Nokto Papirus-Dark Papirus-Light/Papirus-Maia Papirus-Adapta-Maia Papirus-Adapta-Nokto-Maia Papirus-Dark-Maia Papirus-Light-Maia/g' Makefile

#Remove unused icon theme
	rm -R ePapirus

#Change Folders name
	mv Papirus Papirus-Maia
	mv Papirus-Light Papirus-Light-Maia
	mv Papirus-Dark Papirus-Dark-Maia
	mv Papirus-Adapta Papirus-Adapta-Maia
	mv Papirus-Adapta-Nokto Papirus-Adapta-Nokto-Maia

#Change Theme name
	find . -type f -name '*.theme' -exec sed -i '2s/$/-Maia/g' {} \;
	find . -type f -name '*.theme' -exec sed -i '3s/$/ Maia/g' {} \;


}

package() {
	cd $_pkgbase-$pkgver
	make DESTDIR="$pkgdir" install

#Recreate all symlinks to match with Papirus tree
	cd $pkgdir/usr/share/icons
	mkdir -p Papirus
	mkdir -p Papirus-Adapta

	for i in symbolic 64x64 48x48 32x32 24x24 22x22 16x16; do
		ln -sr $pkgdir/usr/share/icons/Papirus-Maia/$i $pkgdir/usr/share/icons/Papirus/$i
		ln -sr $pkgdir/usr/share/icons/Papirus-Adapta-Maia/$i $pkgdir/usr/share/icons/Papirus-Adapta/$i
	done

}
